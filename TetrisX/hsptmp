
	#include "pcbnet2.as"
	#include "d3m.hsp"
	netsilent	//エラーを表示させない

	#define FullScreenMode 1	//通常 1

	#define DX ginfo_winx
	#define DY ginfo_winx*9/16

	#define MinoSize DX/50
	#define MinoDrawAreaX DX/10
	#define MinoDrawAreaY DY/5
	#define DateDrawAreaX MinoDrawAreaX+10*MinoSize+DX/20
	#define DateDrawAreaY
	#define OtherMapDrawAreaX double(DX)/2.2
	#define OtherMapDrawAreaY DY/5
	#define StartMinoX 4
	#define StartMinoY 39

	#define SingleMode 0
	#define MultiMode 1

	#define BUF_MINO 1	//ミノ画像用バッファ

	#define MaxPlayer 8
	#define MaxNameLength 16

	#module
		#deffunc SetMinoColor int p1
			switch p1
				case 0 : color 0,255,255 : swbreak
				case 1 : color 255,255,0 : swbreak
				case 2 : color 0,255,0 : swbreak
				case 3 : color 255,128,0 : swbreak
				case 4 : color 255,0,0 : swbreak
				case 5 : color 0,0,255 : swbreak
				case 6 : color 128,0,255 : swbreak
				case -1 : color 0,0,0 : swbreak
				case -2 : color 128,128,128 : swbreak
				case -3 : color 64,64,64 : swbreak
				default : color 0,0,0 : swbreak
			swend
		return

	#global

	//スクリーン作成
	if (FullScreenMode == 0){ screen 0,960,540 } else { bgscr 0, ginfo_dispx, ginfo_dispy, 0, 0, 0 }
	buffer 1
		celload "./skin/default/mino.png",BUF_MINO,0 : celdiv BUF_MINO,16,16,0,0
		gmode 1

	gsel 0

	randomize

	//ミノデータ
	dim MinoDate, 2,4,4,7
	//Ｉミノ
	MinoDate(0,0,0,0) =  0, 0 : MinoDate(0,1,0,0) = -1, 0 : MinoDate(0,2,0,0) =  1, 0 : MinoDate(0,3,0,0) =  2, 0
	MinoDate(0,0,1,0) =  0, 0 : MinoDate(0,1,1,0) =  0,-1 : MinoDate(0,2,1,0) =  0, 1 : MinoDate(0,3,1,0) =  0, 2
	MinoDate(0,0,2,0) =  0, 0 : MinoDate(0,1,2,0) =  1, 0 : MinoDate(0,2,2,0) = -1, 0 : MinoDate(0,3,2,0) = -2, 0
	MinoDate(0,0,3,0) =  0, 0 : MinoDate(0,1,3,0) =  0,-1 : MinoDate(0,2,3,0) =  0,-2 : MinoDate(0,3,3,0) =  0, 1
	//Ｏミノ
	MinoDate(0,0,0,1) =  0, 0 : MinoDate(0,1,0,1) =  0, 1 : MinoDate(0,2,0,1) =  1, 1 : MinoDate(0,3,0,1) =  1, 0
	MinoDate(0,0,1,1) =  0, 0 : MinoDate(0,1,1,1) = -1, 0 : MinoDate(0,2,1,1) = -1, 1 : MinoDate(0,3,1,1) =  0, 1
	MinoDate(0,0,2,1) =  0, 0 : MinoDate(0,1,2,1) = -1, 0 : MinoDate(0,2,2,1) = -1,-1 : MinoDate(0,3,2,1) =  0,-1
	MinoDate(0,0,3,1) =  0, 0 : MinoDate(0,1,3,1) =  1, 0 : MinoDate(0,2,3,1) =  0,-1 : MinoDate(0,3,3,1) =  1,-1
	//Ｓミノ
	MinoDate(0,0,0,2) =  0, 0 : MinoDate(0,1,0,2) =  0,-1 : MinoDate(0,2,0,2) =  1,-1 : MinoDate(0,3,0,2) = -1, 0
	MinoDate(0,0,1,2) =  0, 0 : MinoDate(0,1,1,2) =  0,-1 : MinoDate(0,2,1,2) =  1, 0 : MinoDate(0,3,1,2) =  1, 1
	MinoDate(0,0,2,2) =  0, 0 : MinoDate(0,1,2,2) =  1, 0 : MinoDate(0,2,2,2) = -1, 1 : MinoDate(0,3,2,2) =  0, 1
	MinoDate(0,0,3,2) =  0, 0 : MinoDate(0,1,3,2) = -1,-1 : MinoDate(0,2,3,2) = -1, 0 : MinoDate(0,3,3,2) =  0, 1
	//Ｌミノ
	MinoDate(0,0,0,3) =  0, 0 : MinoDate(0,1,0,3) =  1,-1 : MinoDate(0,2,0,3) =  1, 0 : MinoDate(0,3,0,3) = -1, 0
	MinoDate(0,0,1,3) =  0, 0 : MinoDate(0,1,1,3) =  0,-1 : MinoDate(0,2,1,3) =  0, 1 : MinoDate(0,3,1,3) =  1, 1
	MinoDate(0,0,2,3) =  0, 0 : MinoDate(0,1,2,3) = -1, 0 : MinoDate(0,2,2,3) =  1, 0 : MinoDate(0,3,2,3) = -1, 1
	MinoDate(0,0,3,3) =  0, 0 : MinoDate(0,1,3,3) = -1,-1 : MinoDate(0,2,3,3) =  0,-1 : MinoDate(0,3,3,3) =  0, 1
	//Ｚミノ
	MinoDate(0,0,0,4) =  0, 0 : MinoDate(0,1,0,4) = -1,-1 : MinoDate(0,2,0,4) =  0,-1 : MinoDate(0,3,0,4) =  1, 0
	MinoDate(0,0,1,4) =  0, 0 : MinoDate(0,1,1,4) =  1,-1 : MinoDate(0,2,1,4) =  1, 0 : MinoDate(0,3,1,4) =  0, 1
	MinoDate(0,0,2,4) =  0, 0 : MinoDate(0,1,2,4) = -1, 0 : MinoDate(0,2,2,4) =  0, 1 : MinoDate(0,3,2,4) =  1, 1
	MinoDate(0,0,3,4) =  0, 0 : MinoDate(0,1,3,4) =  0,-1 : MinoDate(0,2,3,4) = -1, 0 : MinoDate(0,3,3,4) = -1, 1
	//Ｊミノ
	MinoDate(0,0,0,5) =  0, 0 : MinoDate(0,1,0,5) =  1, 0 : MinoDate(0,2,0,5) = -1, 0 : MinoDate(0,3,0,5) = -1,-1
	MinoDate(0,0,1,5) =  0, 0 : MinoDate(0,1,1,5) =  0, 1 : MinoDate(0,2,1,5) =  0,-1 : MinoDate(0,3,1,5) =  1,-1
	MinoDate(0,0,2,5) =  0, 0 : MinoDate(0,1,2,5) =  1, 1 : MinoDate(0,2,2,5) =  1, 0 : MinoDate(0,3,2,5) = -1, 0
	MinoDate(0,0,3,5) =  0, 0 : MinoDate(0,1,3,5) =  0,-1 : MinoDate(0,2,3,5) =  0, 1 : MinoDate(0,3,3,5) = -1, 1
	//Ｔミノ
	MinoDate(0,0,0,6) =  0, 0 : MinoDate(0,1,0,6) = -1, 0 : MinoDate(0,2,0,6) =  1, 0 : MinoDate(0,3,0,6) =  0,-1
	MinoDate(0,0,1,6) =  0, 0 : MinoDate(0,1,1,6) =  1, 0 : MinoDate(0,2,1,6) =  0, 1 : MinoDate(0,3,1,6) =  0,-1
	MinoDate(0,0,2,6) =  0, 0 : MinoDate(0,1,2,6) =  1, 0 : MinoDate(0,2,2,6) = -1, 0 : MinoDate(0,3,2,6) =  0, 1
	MinoDate(0,0,3,6) =  0, 0 : MinoDate(0,1,3,6) = -1, 0 : MinoDate(0,2,3,6) =  0, 1 : MinoDate(0,3,3,6) =  0,-1

	//特殊回転データ
	dim MinoMoveDate, 2,6,8,7
	//Ｉミノ
	MinoMoveDate(0,0,0,0) =  0,-1 : MinoMoveDate(0,1,0,0) = -2,-1 : MinoMoveDate(0,2,0,0) =  1,-1 : MinoMoveDate(0,3,0,0) = -2,-2 : MinoMoveDate(0,4,0,0) =  1, 1 : MinoMoveDate(0,5,0,0) =  0,-1
	MinoMoveDate(0,0,1,0) =  1, 0 : MinoMoveDate(0,1,1,0) = -1, 0 : MinoMoveDate(0,2,1,0) =  2, 0 : MinoMoveDate(0,3,1,0) =  2,-2 : MinoMoveDate(0,4,1,0) = -1, 1 : MinoMoveDate(0,5,1,0) =  1, 0
	MinoMoveDate(0,0,2,0) =  0, 1 : MinoMoveDate(0,1,2,0) =  2, 1 : MinoMoveDate(0,2,2,0) = -1, 1 : MinoMoveDate(0,3,2,0) = -1,-1 : MinoMoveDate(0,4,2,0) =  2, 2 : MinoMoveDate(0,5,2,0) =  0, 1
	MinoMoveDate(0,0,3,0) = -1, 0 : MinoMoveDate(0,1,3,0) =  1, 0 : MinoMoveDate(0,2,3,0) = -2, 0 : MinoMoveDate(0,3,3,0) =  1,-1 : MinoMoveDate(0,4,3,0) = -2, 1 : MinoMoveDate(0,5,3,0) = -1, 0
	MinoMoveDate(0,0,4,0) = -1, 0 : MinoMoveDate(0,1,4,0) =  1, 0 : MinoMoveDate(0,2,4,0) = -2, 0 : MinoMoveDate(0,3,4,0) =  1,-1 : MinoMoveDate(0,4,4,0) = -1, 2 : MinoMoveDate(0,5,4,0) = -1, 0
	MinoMoveDate(0,0,5,0) =  0,-1 : MinoMoveDate(0,1,5,0) = -2,-1 : MinoMoveDate(0,2,5,0) =  1,-1 : MinoMoveDate(0,3,5,0) = -2,-2 : MinoMoveDate(0,4,5,0) =  1, 0 : MinoMoveDate(0,5,5,0) =  0,-1
	MinoMoveDate(0,0,6,0) =  1, 0 : MinoMoveDate(0,1,6,0) = -1, 0 : MinoMoveDate(0,2,6,0) =  2, 0 : MinoMoveDate(0,3,6,0) =  2,-2 : MinoMoveDate(0,4,6,0) = -1, 1 : MinoMoveDate(0,5,6,0) =  1, 0
	MinoMoveDate(0,0,7,0) =  0, 1 : MinoMoveDate(0,1,7,0) = -1, 1 : MinoMoveDate(0,2,7,0) =  2, 1 : MinoMoveDate(0,3,7,0) = -1,-1 : MinoMoveDate(0,4,7,0) =  2, 2 : MinoMoveDate(0,5,7,0) =  0, 1
	//Ｏミノ
	MinoMoveDate(0,0,0,1) =  0,-1 : MinoMoveDate(0,1,0,1) =  0, 0 : MinoMoveDate(0,2,0,1) =  0, 0 : MinoMoveDate(0,3,0,1) =  0, 0 : MinoMoveDate(0,4,0,1) =  0, 0 : MinoMoveDate(0,5,0,1) =  0, 0
	MinoMoveDate(0,0,1,1) =  1, 0 : MinoMoveDate(0,1,1,1) =  0, 0 : MinoMoveDate(0,2,1,1) =  0, 0 : MinoMoveDate(0,3,1,1) =  0, 0 : MinoMoveDate(0,4,1,1) =  0, 0 : MinoMoveDate(0,5,1,1) =  0, 0
	MinoMoveDate(0,0,2,1) =  0, 1 : MinoMoveDate(0,1,2,1) =  0, 0 : MinoMoveDate(0,2,2,1) =  0, 0 : MinoMoveDate(0,3,2,1) =  0, 0 : MinoMoveDate(0,4,2,1) =  0, 0 : MinoMoveDate(0,5,2,1) =  0, 0
	MinoMoveDate(0,0,3,1) = -1, 0 : MinoMoveDate(0,1,3,1) =  0, 0 : MinoMoveDate(0,2,3,1) =  0, 0 : MinoMoveDate(0,3,3,1) =  0, 0 : MinoMoveDate(0,4,3,1) =  0, 0 : MinoMoveDate(0,5,3,1) =  0, 0
	MinoMoveDate(0,0,4,1) = -1, 0 : MinoMoveDate(0,1,4,1) =  0, 0 : MinoMoveDate(0,2,4,1) =  0, 0 : MinoMoveDate(0,3,4,1) =  0, 0 : MinoMoveDate(0,4,4,1) =  0, 0 : MinoMoveDate(0,5,4,1) =  0, 0
	MinoMoveDate(0,0,5,1) =  0,-1 : MinoMoveDate(0,1,5,1) =  0, 0 : MinoMoveDate(0,2,5,1) =  0, 0 : MinoMoveDate(0,3,5,1) =  0, 0 : MinoMoveDate(0,4,5,1) =  0, 0 : MinoMoveDate(0,5,5,1) =  0, 0
	MinoMoveDate(0,0,6,1) =  1, 0 : MinoMoveDate(0,1,6,1) =  0, 0 : MinoMoveDate(0,2,6,1) =  0, 0 : MinoMoveDate(0,3,6,1) =  0, 0 : MinoMoveDate(0,4,6,1) =  0, 0 : MinoMoveDate(0,5,6,1) =  0, 0
	MinoMoveDate(0,0,7,1) =  0, 1 : MinoMoveDate(0,1,7,1) =  0, 0 : MinoMoveDate(0,2,7,1) =  0, 0 : MinoMoveDate(0,3,7,1) =  0, 0 : MinoMoveDate(0,4,7,1) =  0, 0 : MinoMoveDate(0,5,7,1) =  0, 0
	//ＳＬミノ
	MinoMoveDate(0,0,0,2) =  0, 0 : MinoMoveDate(0,1,0,2) = -1, 0 : MinoMoveDate(0,2,0,2) = -1, 1 : MinoMoveDate(0,3,0,2) =  0,-2 : MinoMoveDate(0,4,0,2) = -1,-2 : MinoMoveDate(0,5,0,2) =  0, 0
	MinoMoveDate(0,0,1,2) =  0, 0 : MinoMoveDate(0,1,1,2) = -1, 0 : MinoMoveDate(0,2,1,2) = -1,-1 : MinoMoveDate(0,3,1,2) =  0, 2 : MinoMoveDate(0,4,1,2) = -1, 2 : MinoMoveDate(0,5,1,2) =  0, 0
	MinoMoveDate(0,0,2,2) =  0, 0 : MinoMoveDate(0,1,2,2) =  1, 0 : MinoMoveDate(0,2,2,2) =  1, 1 : MinoMoveDate(0,3,2,2) =  0,-2 : MinoMoveDate(0,4,2,2) =  1,-2 : MinoMoveDate(0,5,2,2) =  0, 0
	MinoMoveDate(0,0,3,2) =  0, 0 : MinoMoveDate(0,1,3,2) =  1, 0 : MinoMoveDate(0,2,3,2) =  1,-1 : MinoMoveDate(0,3,3,2) =  0, 2 : MinoMoveDate(0,4,3,2) =  1, 2 : MinoMoveDate(0,5,3,2) =  0, 0
	MinoMoveDate(0,0,4,2) =  0, 0 : MinoMoveDate(0,1,4,2) =  1, 0 : MinoMoveDate(0,2,4,2) =  1, 1 : MinoMoveDate(0,3,4,2) =  0,-2 : MinoMoveDate(0,4,4,2) =  1,-2 : MinoMoveDate(0,5,4,2) =  0, 0
	MinoMoveDate(0,0,5,2) =  0, 0 : MinoMoveDate(0,1,5,2) = -1, 0 : MinoMoveDate(0,2,5,2) = -1,-1 : MinoMoveDate(0,3,5,2) =  0, 2 : MinoMoveDate(0,4,5,2) = -1, 2 : MinoMoveDate(0,5,5,2) =  0, 0
	MinoMoveDate(0,0,6,2) =  0, 0 : MinoMoveDate(0,1,6,2) = -1, 0 : MinoMoveDate(0,2,6,2) = -1, 1 : MinoMoveDate(0,3,6,2) =  0,-2 : MinoMoveDate(0,4,6,2) = -1,-2 : MinoMoveDate(0,5,6,2) =  0, 0
	MinoMoveDate(0,0,7,2) =  0, 0 : MinoMoveDate(0,1,7,2) =  1, 0 : MinoMoveDate(0,2,7,2) =  1,-1 : MinoMoveDate(0,3,7,2) =  0, 2 : MinoMoveDate(0,4,7,2) =  1, 2 : MinoMoveDate(0,5,7,2) =  0, 0
	//ＺＪＴミノ
	MinoMoveDate(0,0,0,3) =  0, 0 : MinoMoveDate(0,1,0,3) = -1, 0 : MinoMoveDate(0,2,0,3) = -1, 1 : MinoMoveDate(0,3,0,3) =  0,-2 : MinoMoveDate(0,4,0,3) = -1,-2 : MinoMoveDate(0,5,0,3) =  0, 0
	MinoMoveDate(0,0,1,3) =  0, 0 : MinoMoveDate(0,1,1,3) = -1, 0 : MinoMoveDate(0,2,1,3) = -1,-1 : MinoMoveDate(0,3,1,3) =  0, 2 : MinoMoveDate(0,4,1,3) = -1, 2 : MinoMoveDate(0,5,1,3) =  0, 0
	MinoMoveDate(0,0,2,3) =  0, 0 : MinoMoveDate(0,1,2,3) =  1, 0 : MinoMoveDate(0,2,2,3) =  1, 1 : MinoMoveDate(0,3,2,3) =  0,-2 : MinoMoveDate(0,4,2,3) =  1,-2 : MinoMoveDate(0,5,2,3) =  0, 0
	MinoMoveDate(0,0,3,3) =  0, 0 : MinoMoveDate(0,1,3,3) =  1, 0 : MinoMoveDate(0,2,3,3) =  1,-1 : MinoMoveDate(0,3,3,3) =  0, 2 : MinoMoveDate(0,4,3,3) =  1, 2 : MinoMoveDate(0,5,3,3) =  0, 0
	MinoMoveDate(0,0,4,3) =  0, 0 : MinoMoveDate(0,1,4,3) =  1, 0 : MinoMoveDate(0,2,4,3) =  1, 1 : MinoMoveDate(0,3,4,3) =  0,-2 : MinoMoveDate(0,4,4,3) =  1,-2 : MinoMoveDate(0,5,4,3) =  0, 0
	MinoMoveDate(0,0,5,3) =  0, 0 : MinoMoveDate(0,1,5,3) = -1, 0 : MinoMoveDate(0,2,5,3) = -1,-1 : MinoMoveDate(0,3,5,3) =  0, 2 : MinoMoveDate(0,4,5,3) = -1, 2 : MinoMoveDate(0,5,5,3) =  0, 0
	MinoMoveDate(0,0,6,3) =  0, 0 : MinoMoveDate(0,1,6,3) = -1, 0 : MinoMoveDate(0,2,6,3) = -1, 1 : MinoMoveDate(0,3,6,3) =  0,-2 : MinoMoveDate(0,4,6,3) = -1,-2 : MinoMoveDate(0,5,6,3) =  0, 0
	MinoMoveDate(0,0,7,3) =  0, 0 : MinoMoveDate(0,1,7,3) =  1, 0 : MinoMoveDate(0,2,7,3) =  1,-1 : MinoMoveDate(0,3,7,3) =  0, 2 : MinoMoveDate(0,4,7,3) =  1, 2 : MinoMoveDate(0,5,7,3) =  0, 0

	dim RenTable,15
	RenTable(0) = 0,0,1,1,2,2,3,3,4,4,4,5,5,5,5

	dim AttackTable,5
	AttackTable(0) = 0,0,1,2,4

	sdim IPAddress,32,MaxPlayer
	MyIPAddress = ""
	ownip MyIPAddress
	Port = 50000
	dim SockID,MaxPlayer

	dim Rate,MaxPlayer
	sdim Name,MaxNameLength,MaxPlayer
	Name(0) = "Guest"

	dim PlayerNowDate,MaxPlayer

	sdim NowMapDate,1024,MaxPlayer

	DrawFPS = 0


	/* タイトルとかいろいろ作る */

*menu
	GameMode = SingleMode

	cls
	objmode 2
	objsize DX/10,DY/50
	font msgothic,DX/100
	mes "名前" : input Name(0),,DX/60,MaxNameLength
	objsize DX/20,DX/30
	mes "\nシングルプレイ"
	button goto "スタート",*single
	mes "\nマルチプレイ"
	button goto "募集する",*serverwait
	button goto "参加する",*clientset
	mes ""
	button goto "設定",*setting
	mes ""
	button goto "終了する",*escmenu
	mes ""

*menuloop

	await 1

	goto *menuloop



*setting
	cls
	font msgothic,DX/50 : mes "ゲーム設定"
	font msgothic,DX/80
	pos DX/80,DX/40
	mes "FPS表示"
	objsize DX/10,DX/30 : combox DrawFPS,DX/30,"表示しない\n表示する"
	objsize DX/20,DX/30 : button goto "戻る",*menu
	stop



//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



*serverwait
	GameMode = MultiMode
	Mynum = 0
	cls : objsize DX/20,DX/20 : font msgothic,DX/100
	mes "待機中です..."
	mes "あなたのIPアドレスは「"+MyIPAddress+"」です。"
	mes "現在の参加者は1人です。"
	button goto "スタート",*serverstart
	NowPlayer = 1
	udpsock SockID(0),Port

*serverloop
	udpcheck Packet,SockID(0)
	repeat Packet
		sdim Buf,64 : sdim Info,64
		udpget Buf,64,SockID(0)
		udpinfo Info,SockID(0)

		if (instr(Buf,0,"nam0") == 0){
			Name(NowPlayer) = strmid(Buf,4,MaxNameLength)
			udpsock SockID(NowPlayer),Port+NowPlayer
			udpsendto SockID(NowPlayer),strmid(Info,0,instr(Info,0,":")),Port
			repeat NowPlayer-1,1
				udpput "nam"+NowPlayer+Name(NowPlayer),SockID(cnt)
				udpput "ipa"+NowPlayer+strmid(Info,0,instr(Info,0,":")),SockID(cnt)
			loop
			udpput "num"+NowPlayer,SockID(NowPlayer)
			NowPlayer++

			cls : objsize DX/20,DX/20 : font msgothic,DX/100
			mes "待機中です..."
			mes "あなたのIPアドレスは「"+MyIPAddress+"」です。"
			mes "現在の参加者は"+NowPlayer+"人です。"
			button goto "スタート",*serverstart
		}
	loop

	await 1
	goto *serverloop

*serverstart
	randomize
	RandomNumber = rnd(1000)
	
	repeat NowPlayer-1,1
		udpput "sta"+strf("%03d",RandomNumber),SockID(cnt)
	loop

	goto *start



//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



*clientset
	cls : objsize DX/20,DX/20 : font msgothic,DX/100
	mes "IPアドレスを入力してください。"
	ServerIPAddress = ""
	input ServerIPAddress,DX/10,DY/40
	button goto "参加する",*clientwait
	button goto "戻る",*menu
	stop


*clientwait
	GameMode = MultiMode
	NowPlayer = 2
	cls : mes "待機中です、しばらくお待ちください"
	udpsock SockID(0),Port
	udpsendto SockID(0),ServerIPAddress,Port
	udpput "nam0"+Name(0),SockID(0)

*clientloop
	StartFlag = 0
	udpcheck Packet,SockID(0)
	repeat Packet
		sdim Buf,64 : sdim Info,64
		udpget Buf,64,SockID(0)
		udpinfo Info,SockID(0)

		if (instr(Buf,0,"num") == 0){
			Mynum = int(strmid(Buf,3,1))
			Name(Mynum) = Name(0)
		}
		if (instr(Buf,0,"nam") == 0){
			NowPlayer++
			Name(int(strmid(Buf,3,1))) == strmid(Buf,3,MaxNameLength)
		}
		if (instr(Buf,0,"ipa") == 0){
			udpsock SockID(int(strmid(Buf,3,1))),Port+int(strmid(Buf,3,1))
			udpsendto SockID(int(strmid(Buf,3,1))),strmid(Buf,4,16),Port
			if (strmid(Buf,3,1) == "0"){
				udpput "nam"+Mynum+Name(Mynum),SockID(int(strmid(Buf,3,1)))
				udpput "ipa"+Mynum+MyIPAddress,SockID(int(strmid(Buf,3,1)))
			}
		}
		if (instr(Buf,0,"sta") == 0){
			RandomNumber = int(strmid(Buf,3,3))
			StartFlag = 1
		}
	loop

	if (StartFlag == 1){ goto *start }

	await 1
	goto *clientloop



//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



*single
	randomize
	RandomNumber = rnd(1000)



//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



*start
	cls
	randomize RandomNumber
	MapSizeX = 10 : MapSizeY = 61

	FTime = d3timer()

	dim MinoLine,7,2
	repeat 7
		MinoLine(cnt,0) = cnt
		MinoLine(cnt,1) = cnt
	loop
	MinoCount = 0 : MinoLineCount = 0
	gosub *shuffle
	MinoR = 0
	MinoX = StartMinoX : MinoY = StartMinoY
	MinoT = MinoLine(MinoCount,MinoLineCount)
	MainCount = 0
	DownCount = 0 : DownTime = 50 : MaxDownTime = 100
	MarginTime = 100
	MinoMoveFlag = 0
	HoldMino = -1 : HoldFlag = 0
	SoftDropSpeed = 20
	ClearLine = 0
	Ren = 0 : RenFlag = 0
	SplitClear = 0	//0:なし 1:11 2:12 3:21 
	SplitClearFlag = 0 : Split1ClearFlag = 0 : Split2ClearFlag = 0
	TspinFlag = 0 : TspinClear = 0
	BacktobackFlag = 0 : BacktobackClear = 0
	NowTime = 0
	SumAttack = 0
	SumDamage = 0 : DamageMarginSTime = 0 : DamageMargin = 3000
	dim Damage,100 : DamageCount = 0
	RexRate = 0.50

	//チーム戦実装でたぶん変える
	repeat NowPlayer
		PlayerNowDate(cnt) = 1
	loop
	

	KeyCodeMU = 38 : KeyCodeMD = 40
	KeyCodeMR = 39 : KeyCodeML = 37
	KeyCodeRR = 88 : KeyCodeRL = 90
	KeyCodeH = 67 : KeyCodeS = 86
	KeyChargeTime = 10

	dim MapDate, MapSizeX,MapSizeY
	Cou0 = 0
	repeat MapSizeX
		Cou1 = 0
		repeat MapSizeY
			MapDate(Cou0,Cou1) = -1
			Cou1++
		loop
		Cou0++
	loop

	dim MapMarginTime, MapSizeX,MapSizeY,2




//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



	BMinoX = MinoX : BMinoY = MinoY : BMinoR = MinoR
	DMinoX = MinoX : DMinoY = MinoY : DMinoR = MinoR
	gosub *remmino
	gosub *putmino

*main	//メインループ

	gosub *downmino
	gosub *keycheck

	gosub *gservercheck
	gosub *timecheck
	gosub *draw

	if (GameMode == MultiMode){
		gosub *recvdate
		gosub *damagecheck
		if (MainCount\60 == 0){
			gosub *putdate
		}
	}

	MainCount++

	await 1000/60	//FPS 60

	goto *main



//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//==//



//ミノの降下
*downmino
	gosub *downcheck

	//まだ落とせるかの分岐
	if (KMinoY != MinoY){
		DownCount++
		MapMarginTime(MinoX,MinoY,1)++
	} else {
		DownCount = 0
		MapMarginTime(MinoX,MinoY,0)++
	}

	//一定時間で下に落とす
	if ((DownCount >= DownTime) | (MapMarginTime(MinoX,MinoY,1) >= MaxDownTime)){
		if (KMinoY == MinoY){ MapMarginTime(MinoX,MinoY,0) = MarginTime }
		DownCount = 0
		MinoY++
		TspinFlag = 0
	}

	//ミノを置く
	if (MapMarginTime(MinoX,MinoY,0) >= MarginTime){
		HoldFlag = 0
		gosub *linecheck
		PerfectClear = 0 : PerfectClearFlag = 1
		repeat MapSizeX
			if (MapDate(cnt,MapSizeY-1) != -1){
				PerfectClearFlag = 0
			}
		loop
		if (PerfectClearFlag == 1){ PerfectClear = 1 }
		gosub *bornmino
		gosub *putcheck
		gosub *attackprocess

		dim MapMarginTime, MapSizeX,MapSizeY,2

		//置けないならゲームオーバー
		if (Result == 1){ gosub *gameover }
	}

	return



//どこまで落とせるかの確認
*downcheck
	KMinoX = MinoX : KMinoY = MinoY

	gosub *remmino
	repeat
		gosub *putcheck
		if (Result == 1){ break }
		KMinoY++
	loop

	KMinoY--

	gosub *putmino
	

	return



*gservercheck
	Cou0 = 0
	repeat 4
		MapDate(DMinoX+MinoDate(0,Cou0,DMinoR,MinoT),DMinoY+MinoDate(1,Cou0,DMinoR,MinoT)) = -1
		Cou0++
	loop

	gosub *downcheck

	DMinoX = KMinoX : DMinoY = KMinoY : DMinoR = MinoR

	Cou0 = 0
	repeat 4
		if (MapDate(DMinoX+MinoDate(0,Cou0,BMinoR,MinoT),DMinoY+MinoDate(1,Cou0,BMinoR,MinoT)) == -1){
			MapDate(DMinoX+MinoDate(0,Cou0,BMinoR,MinoT),DMinoY+MinoDate(1,Cou0,BMinoR,MinoT)) = -2
		}
		Cou0++
	loop

	return



*timecheck
	NowTime = d3timer()-FTime

	return



*bornmino
	MinoCount++
	if (MinoCount == 7){
		gosub *shuffle
		MinoLineCount = 1-MinoLineCount
		MinoCount = 0
	}
	MinoT = MinoLine(MinoCount,MinoLineCount)
	MinoR = 0
	MinoX = StartMinoX : MinoY = StartMinoY
	DownCount = 0
	TspinFlag = 0
	KMinoX = MinoX : KMinoY = MinoY : KMinoR = MinoR
	BMinoX = MinoX : BMinoY = MinoY : BMinoR = MinoR
	DMinoX = MinoX : DMinoY = MinoY : DMinoR = MinoR

	return



*remmino
	Cou0 = 0
	repeat 4
		MapDate(BMinoX+MinoDate(0,Cou0,BMinoR,MinoT),BMinoY+MinoDate(1,Cou0,BMinoR,MinoT)) = -1
		Cou0++
	loop

	return



*putmino
	Cou0 = 0
	repeat 4
		MapDate(MinoX+MinoDate(0,Cou0,MinoR,MinoT),MinoY+MinoDate(1,Cou0,MinoR,MinoT)) = MinoT
		Cou0++
	loop

	BMinoX = MinoX : BMinoY = MinoY : BMinoR = MinoR

	return



*draw
	redraw 0
	color 255,255,255 : boxf

	//ミノの描写
	Cou0 = 0
	repeat 20
		Cou1 = 0
		repeat MapSizeX
			SetMinoColor MapDate(Cou1,Cou0+MapSizeY-20)
			boxf MinoDrawAreaX+Cou1*MinoSize,MinoDrawAreaY+Cou0*MinoSize,MinoDrawAreaX+Cou1*MinoSize+MinoSize,MinoDrawAreaY+Cou0*MinoSize+MinoSize
			Cou1++
		loop
		Cou0++
	loop

	//マス目の線
	color 64,64,64
	repeat 19,1 : line MinoDrawAreaX,MinoDrawAreaY+cnt*MinoSize,MinoDrawAreaX+10*MinoSize,MinoDrawAreaY+cnt*MinoSize : loop
	repeat 9,1 : line MinoDrawAreaX+cnt*MinoSize,MinoDrawAreaY,MinoDrawAreaX+cnt*MinoSize,MinoDrawAreaY+20*MinoSize : loop

	//ダメージゲージ
	color 0,0,0 : boxf MinoDrawAreaX+10*MinoSize+DX/60,MinoDrawAreaY,MinoDrawAreaX+10*MinoSize+DX/36,MinoDrawAreaY+20*MinoSize
	if (SumDamage > 20){
		color 255,0,0 : boxf MinoDrawAreaX+10*MinoSize+DX/60,MinoDrawAreaY,MinoDrawAreaX+10*MinoSize+DX/36,MinoDrawAreaY+20*MinoSize
	} else {
		color 255,0,0 : boxf MinoDrawAreaX+10*MinoSize+DX/60,MinoDrawAreaY+(20-SumDamage)*MinoSize,MinoDrawAreaX+10*MinoSize+DX/36,MinoDrawAreaY+20*MinoSize
	}

	//ネクストミノの描写
	color 0,0,0 : font msgothic,DX/50 : pos DateDrawAreaX,DY/7 : mes "NEXT"
	font msgothic, MinoSize
	Cou0 = MinoCount
	Cou1 = MinoLineCount
	repeat 5
		Cou0++
		if (Cou0 == 7){ Cou0 = 0 : Cou1 = 1-Cou1 }

		pos DateDrawAreaX,DY/5+MinoSize*3*cnt
		celput BUF_MINO,MinoLine(Cou0,Cou1)+7,double(MinoSize)*3/16,double(MinoSize)*3/16
	loop

	//ホールドミノの描写
	color 0,0,0 : font msgothic,DX/50 : pos DateDrawAreaX,double(DY)/1.3 : mes "HOLD"
	pos DateDrawAreaX,double(DY)/1.2
	if (HoldMino != -1){
		celput BUF_MINO,HoldMino+7,double(MinoSize)*3/16,double(MinoSize)*3/16
	}

	//データの描写
	if (Ren > 0){ color 0,0,0 : font msgothic,DX/50 : pos DX/40,DY/3 : mes str(Ren)+"REN" }
	if (BacktobackClear == 1){ color 0,0,0 : font msgothic,DX/80 : pos DX/100,double(DY)/2.1 : mes "Back to Back" }
	if (ClearLine == 4){ color 0,0,0 : font msgothic,DX/60 : pos DX/40,DY/2 : mes "Tetris" }
	if ((TspinFlag > 0) | (TspinClear > 0)){ color 0,0,0 : font msgothic,DX/70 : pos DX/70,DY/2 : mes "T-Spin" }
	switch TspinClear
		case 1
			color 0,0,0 : font msgothic,DX/70 : pos DX/30,double(DY)/1.9 : mes "Single"
		swbreak
		case 2
			color 0,0,0 : font msgothic,DX/70 : pos DX/30,double(DY)/1.9 : mes "Double"
		swbreak
		case 3
			color 0,0,0 : font msgothic,DX/70 : pos DX/30,double(DY)/1.9 : mes "Triple"
		swbreak
		case 4
			color 0,0,0 : font msgothic,DX/70 : pos DX/30,double(DY)/1.9 : mes "Mini"
		swbreak
	swend

	color 0,0,0 : font msgothic,DX/50 : pos DateDrawAreaX,DY/10 : mes str(int((NowTime/1000)/60))+" : "+strf("%02d",int((NowTime/1000)\60))
	color 0,0,0 : pos DateDrawAreaX+DX/10,double(DY)/10 : mes "APM : "+str(Apm)
	pos MinoDrawAreaX,MinoDrawAreaY-DX/20 : mes Name(Mynum) : mes "Rate "+Rate(Mynum)
	color 0,0,0 : pos 0,0
	if (DrawFPS == 1){ mes "FPS : "+d3getfps() }
	if (CanBackFlag == 1){ mes "Enterでもう一度\nEscで終了" }

	//ほかのプレイヤーの盤面描写
	Cou0 = 0
	font msgothic,DX/100
	repeat NowPlayer
		color 0,0 : pos OtherMapDrawAreaX+(Cou0\4)*(DX/8),OtherMapDrawAreaY+(Cou0/4)*(double(DY)/2.3)-DX/50
		mes Name(Cou0)
		mes "Rate "+Rate(Cou0)
		repeat 20*MapSizeX
			SetMinoColor int(strmid(NowMapDate(Cou0),cnt*2,2))
			boxf OtherMapDrawAreaX+(Cou0\4)*(DX/8)+(cnt\MapSizeX)*(DX/100),OtherMapDrawAreaY+(Cou0/4)*(double(DY)/2.3)+(cnt/MapSizeX)*(DX/100),OtherMapDrawAreaX+(Cou0\4)*(DX/8)+(cnt\MapSizeX)*(DX/100)+(DX/100),OtherMapDrawAreaY+(Cou0/4)*(double(DY)/2.3)+(cnt/MapSizeX)*(DX/100)+(DX/100)
		loop
		Cou0++
	loop


	redraw 1

	return



*attackprocess
	Attack = 0
	Attack += AttackTable(ClearLine)
	if (Ren < 15){ Attack += RenTable(Ren) } else { Attack += RenTable(14) }
	switch TspinClear
		case 1
			Attack += 2
		swbreak
		case 2
			Attack += 3
		swbreak
		case 3
			Attack += 4
		swbreak
	swend
	if (PerfectClear == 1){ Attack += 10 }
	if (BacktobackClear == 1){ Attack++ }

	if ((Attack > 0) & (DamageMarginSTime != 0)){
		DamageMarginSTime = d3timer()
		repeat DamageCount
			if (Attack >= Damage(DamageCount-1)){
				SumDamage -= Damage(DamageCount-1)
				if (SumDamage < 0){ SumDamage = 0 }
				Attack -= Damage(DamageCount-1)
				Damage(DamageCount-1) = 0
				DamageCount--
			} else {
				Damage(DamageCount-1) -= Attack
				SumDamage -= Attack
				if (SumDamage < 0){ SumDamage = 0 }
				Attack = 0
				break
			}
			if (Attack == 0){ break }
		loop
	}

	LivePlayer = 0
	repeat NowPlayer
		if (PlayerNowDate(cnt) > 0){
			LivePlayer++
		}
	loop

	if ((Attack > 0) & (GameMode == MultiMode) & (LivePlayer > 1)){
		if (AttackMode == 0){
			toAttack = 0
			repeat
				toAttack = rnd(LivePlayer)
				if ((toAttack != Mynum) & (PlayerNowDate(toAttack) > 0)){ break }
			loop
			udpput "att"+Mynum+Attack,SockID(toAttack)
		}
		if (AttackMode == 1){
			repeat NowPlayer
				if ((cnt != Mynum) & (PlayerNowDate(toAttack) > 0)){
					udpput "att"+Mynum+Attack,SockID(cnt)
				}
			loop
		}
	}

	SumAttack += Attack
	if (NowTime/1000 == 0){
		Apm = SumAttack
	} else {
		Apm = int(double(SumAttack)*60/(NowTime/1000))
	}

	return



*putdate
	Cou0 = 0 : NowMapDate(Mynum) = ""
	repeat 20
		Cou1 = 0
		repeat MapSizeX
			NowMapDate(Mynum) += strf("%02d",MapDate(Cou1,Cou0+MapSizeY-20))
			Cou1++
		loop
		Cou0++
	loop

	repeat NowPlayer
		if (cnt != Mynum){
			udpput "map"+str(Mynum)+NowMapDate(Mynum),SockID(cnt)
		}
	loop

	return



*recvdate
	Cou0 = 0
	repeat NowPlayer
		if (cnt != Mynum){
			udpcheck Packet,SockID(Cou0)
			repeat Packet
				sdim Buf,64 : sdim Info,64
				udpget Buf,64,SockID(Cou0)
				udpinfo Info,SockID(Cou0)

				if (instr(Buf,0,"att") == 0){
					SumDamage += int(strmid(Buf,4,2))
					Damage(DamageCount) = int(strmid(Buf,4,2))
					DamageCount++
				}
				if (instr(Buf,0,"die") == 0){
					PlayerNowDate(int(strmid(Buf,3,1))) = 0
				}
				if (instr(Buf,0,"map") == 0){
					NowMapDate(int(strmid(Buf,3,1))) = strmid(Buf,4,512)
				}
				if (instr(Buf,0,"sta") == 0){
					RandomNumber = int(strmid(Buf,3,3))
					StartFlag = 1
				}
			loop
		}
		Cou0++
	loop

	return



*damagecheck
	if ((SumDamage > 0) & (DamageMarginSTime == 0)){ DamageMarginSTime = d3timer() }
	if ((d3timer()-DamageMarginSTime > DamageMargin) & (DamageMarginSTime != 0)){
		gosub *remmino
		MinoY -= SumDamage
		KMinoX = MinoX : KMinoY = MinoY
		repeat
			gosub *putcheck
			if (Result == 1){ break }
			KMinoY++
		loop
		KMinoY--
		DMinoX = KMinoX : DMinoY = KMinoY : DMinoR = MinoR
		Cou0 = 0
		repeat 4
			MapDate(DMinoX+MinoDate(0,Cou0,DMinoR,MinoT),DMinoY+MinoDate(1,Cou0,DMinoR,MinoT)) = -1
			Cou0++
		loop
		repeat SumDamage
			Cou1 = 0
			repeat MapSizeY-1
				repeat MapSizeX
					MapDate(cnt,Cou1) = MapDate(cnt,Cou1+1)
				loop
				Cou1++
			loop
		loop

		Cou0 = DamageCount-1 : Cou1 = MapSizeY-1
		repeat
			Hole = rnd(MapSizeX)
			repeat Damage(Cou0)
				repeat MapSizeX
					if (cnt != Hole){ MapDate(cnt,Cou1) = 0 } else { MapDate(cnt,Cou1) = -1 }
				loop
				Cou1--
			loop
			Cou0--
			if (Cou0 == -1){ break }
		loop

		dim Damage,100
		SumDamage = 0
		DamageMarginSTime = 0
		DamageCount = 0

		BMinoX = MinoX : BMinoY = MinoY : BMinoR = MinoR
		gosub *downcheck
		DMinoX = KMinoX : DMinoY = KMinoY : DMinoR = MinoR
	}

	return



*keycheck
	getkey KeyRR,KeyCodeRR
	if (KeyRR == 1){
		CKeyRR++
		if (CKeyRR == 1){
			KMinoR = MinoR
			if (MinoR == 3){ MinoR = 0 } else { MinoR++ }
			gosub *remmino
			switch MinoT/2
				case 0
					if (MinoT == 0){
						MinoMoveFlag = 0
					} else {
						MinoMoveFlag = 1
					}
				swbreak
				case 1 : MinoMoveFlag = 2 : swbreak
				case 2 : MinoMoveFlag = 3 : swbreak
				case 3 : MinoMoveFlag = 3 : swbreak
			swend
			Cou1 = 0
			repeat 5
				KMinoX = MinoX+MinoMoveDate(0,Cou1,MinoR,MinoMoveFlag)
				KMinoY = MinoY+MinoMoveDate(1,Cou1,MinoR,MinoMoveFlag)
				gosub *putcheck
				if (Result == 0){
					MinoX = KMinoX : MinoY = KMinoY
					break
				}
				Cou1++
			loop
			if (Result == 1){ MinoR = KMinoR }
			gosub *tspincheck
			gosub *putmino
		}
	} else {
		CKeyRR = 0
	}
	getkey KeyRL,KeyCodeRL
	if (KeyRL == 1){
		CKeyRL++
		if (CKeyRL == 1){
			KMinoR = MinoR
			if (MinoR == 0){ MinoR = 3 } else { MinoR-- }
			gosub *remmino
			switch MinoT/2
				case 0
					if (MinoT == 0){
						MinoMoveFlag = 0
					} else {
						MinoMoveFlag = 1
					}
				swbreak
				case 1 : MinoMoveFlag = 2 : swbreak
				case 2 : MinoMoveFlag = 3 : swbreak
				case 3 : MinoMoveFlag = 3 : swbreak
			swend
			Cou1 = 0
			repeat 5
				KMinoX = MinoX+MinoMoveDate(0,Cou1,MinoR+4,MinoMoveFlag)
				KMinoY = MinoY+MinoMoveDate(1,Cou1,MinoR+4,MinoMoveFlag)
				gosub *putcheck
				if (Result == 0){
					MinoX = KMinoX : MinoY = KMinoY
					break
				}
				Cou1++
			loop
			if (Result == 1){ MinoR = KMinoR }
			gosub *tspincheck
			gosub *putmino
		}
	} else {
		CKeyRL = 0
	}

	getkey KeyMR,KeyCodeMR
	if (KeyMR == 1){
		CKeyMR++
		if ((CKeyMR == 1) | (CKeyMR > KeyChargeTime)){
			KMinoX = MinoX : KMinoY = MinoY
			KMinoX++
			gosub *remmino
			gosub *putcheck
			if (Result == 0){
				MinoX = KMinoX
				MapMarginTime(MapX,MapY,0) = int(RexRate*MapMarginTime(MinoX,MinoY,0))
			}
			gosub *putmino
		}
	} else {
		CKeyMR = 0
	}
	getkey KeyML,KeyCodeML
	if (KeyML == 1){
		CKeyML++
		if ((CKeyML == 1) | (CKeyML > KeyChargeTime)){
			KMinoX = MinoX : KMinoY = MinoY
			KMinoX--
			gosub *remmino
			gosub *putcheck
			if (Result == 0){
				MinoX = KMinoX
				MapMarginTime(MapX,MapY,0) = int(RexRate*MapMarginTime(MinoX,MinoY,0))
			}
			gosub *putmino
		}
	} else {
		CKeyML = 0
	}
	getkey KeyMD,KeyCodeMD
	if (KeyMD == 1){
		DownCount += SoftDropSpeed
	}
	getkey KeyMU,KeyCodeMU
	if (KeyMU == 1){
		CKeyMU++
		if (CKeyMU == 1){
			gosub *downcheck
			MinoX = KMinoX : MinoY = KMinoY
			MapMarginTime(MinoX,MinoY,0) = MarginTime
		}
	} else {
		CKeyMU = 0
	}

	getkey KeyH,KeyCodeH
	if (KeyH == 1){
		CKeyH++
		if ((CKeyH == 1) & (HoldFlag == 0)){
			HoldFlag = 1
			if (HoldMino == -1){
				gosub *remmino
				HoldMino = MinoT
				BMinoX = DMinoX : BMinoY = DMinoY : BMinoR = DMinoR
				gosub *remmino
				gosub *bornmino
			} else {
				gosub *remmino
				BMinoX = DMinoX : BMinoY = DMinoY : BMinoR = DMinoR
				gosub *remmino
				KHoldMino = HoldMino
				HoldMino = MinoT : MinoT = KHoldMino
				MinoR = 0
				MinoX = StartMinoX : MinoY = StartMinoY
				DownCount = 0
				TspinFlag = 0
				KMinoX = MinoX : KMinoY = MinoY : KMinoR = MinoR
				BMinoX = MinoX : BMinoY = MinoY : BMinoR = MinoR
				DMinoX = MinoX : DMinoY = MinoY : DMinoR = MinoR
			}
		}
	} else {
		CKeyH = 0
	}

	return



*linecheck
	Cou0 = MapSizeY-1
	SplitClear = 0
	SplitClearFlag = 0 : Split1ClearFlag = 0 : Split2ClearFlag = 0
	if (ClearLine > 0){ RenFlag = 1 } else { RenFlag = 0 }
	ClearLine = 0
	TspinClear = 0 : BacktobackClear = 0
	repeat MapSizeY
		LineClearFlag = 1
		repeat MapSizeX
			if (MapDate(cnt,Cou0) == -1){
				LineClearFlag = 0
				break
			}
		loop
		if (LineClearFlag == 1){
			Cou1 = Cou0 : ClearLine++
			repeat Cou0-1
				repeat MapSizeX
					MapDate(cnt,Cou1) = MapDate(cnt,Cou1-1)
				loop
				Cou1--
			loop
			if (SplitClearFlag == 0){ Split1ClearFlag++ } else { Split2ClearFlag++ }
			Cou0++
		} else {
			if ((SplitClearFlag == 0) & (Split1ClearFlag > 0)){
				SplitClearFlag = 1
			}
		}
		Cou0--
	loop
	if ((RenFlag == 1) & (ClearLine > 0)){
		Ren++
	} else {
		Ren = 0
	}
	if ((Split1ClearFlag > 0) & (Split2ClearFlag > 0)){
		switch Split1ClearFlag-Split2ClearFlag
			case 0 : SplitClear = 1 : swbreak
			case 1 : SplitClear = 2 : swbreak
			case -1 : SplitClear = 3 : swbreak
		swend
	}
	if (TspinFlag != 0){
		switch ClearLine
			case 1
				if (TspinFlag == 1){ TspinClear = 1 } else { TspinClear = 4 }
			swbreak
			case 2 : TspinClear = 2 : swbreak
			case 3 : TspinClear = 3 : swbreak
		swend
	}
	if ((ClearLine == 4) | (TspinClear > 0)){
		if (BacktobackFlag == 1){ BacktobackClear = 1 }
		BacktobackFlag = 1
	} else {
		if (ClearLine > 0){ BacktobackFlag = 0 }
	}
	if (TspinFlag != 0){
		BacktobackFlag = 1
	}

	return



*putcheck
	Result = 0
	Cou0 = 0
	K1MinoX = KMinoX : K1MinoY = KMinoY
	repeat 4
		K1MinoX = KMinoX+MinoDate(0,Cou0,MinoR,MinoT)
		K1MinoY = KMinoY+MinoDate(1,Cou0,MinoR,MinoT)
		if ((K1MinoX >= MapSizeX) | (K1MinoY >= MapSizeY) | (K1MinoX < 0) | (K1MinoY <= 0)){ Result = 1 }
		else { if (MapDate(K1MinoX,K1MinoY) >= 0){ Result = 1 } }
		Cou0++
	loop

	return



*tspincheck
	if (MinoT == 6){
		TspinFlag = 0
		switch MinoX
			case 0
				if ((MapDate(1,MinoY-1) >= 0) | (MapDate(1,MinoY+1) >= 0)){ TspinFlag = 2 }
			swbreak
			case MapSizeX-1
				if ((MapDate(MapSizeX-2,MinoY-1) >= 0) | (MapDate(MapSizeX-2,MinoY+1) >= 0)){ TspinFlag = 2 }
			swbreak
			default
				if (MinoY == MapSizeY-1){
					if ((MapDate(MinoX-1,MapSizeY-2) >= 0) | (MapDate(MinoX+1,MapSizeY-2) >= 0)){ TspinFlag = 2 }
				} else {
					Cou0 = 0
					repeat 4
						if (MapDate(MinoX+(cnt/2)*2-1,MinoY+(cnt\2)*2-1) >= 0){
							Cou0++
						}
					loop
					if (Cou0 >= 3){
						if (MapDate(MinoX+(MinoR\2)*(MinoR/2*2-1),MinoY+(1-MinoR\2)*(1-MinoR/2*2)) >= 0){
							TspinFlag = 2
						} else {
							TspinFlag = 1
						}
					}
				}
			swbreak
		swend
	}

	return



*shuffle
	repeat 7
		r = cnt + rnd(7 - cnt)
		temp = MinoLine(r,MinoLineCount)
		MinoLine(r,MinoLineCount) = MinoLine(cnt,MinoLineCount)
		MinoLine(cnt,MinoLineCount) = temp
	loop
	return



*gameover
	PlayerNowDate(Mynum) = 0
	repeat NowPlayer
		if (cnt != Mynum){
			udpput "die"+Mynum,SockID(cnt)
		}
	loop

	Cou0 = 0
	repeat 20
		Cou1 = 0
		repeat MapSizeX
			if (MapDate(Cou1,Cou0+MapSizeY-20) >= 0){ MapDate(Cou1,Cou0+MapSizeY-20) = -3 }
			Cou1++
		loop
		Cou0++
	loop

	if (GameMode == MultiMode){
		gosub *putdate
	}
	CanBackFlag = 0

*dieloop
	if (GameMode == MultiMode){
		gosub *recvdate
	}
	gosub *draw

	LivePlayer = 0
	repeat NowPlayer
		if (PlayerNowDate(cnt) > 0){
			LivePlayer++
		}
	loop

	stick key
	if (key == 128){ end }
	if (GameMode == MultiMode){
		if (Mynum == 0){ CanBackFlag = 1 }
		if ((LivePlayer <= 1) & (key == 32)){ goto *serverstart }
		if (StartFlag == 1){ goto *start }
	} else {
		CanBackFlag = 1
		if (key == 32){
			CanBackFlag = 0
			goto *start
		}
	}

	await 1000/60

	goto *dieloop



*escmenu
	end
