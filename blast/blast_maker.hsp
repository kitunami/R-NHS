	;おーばーてくのろじー
	#include "obaq.as"

*reset

	screen 0
	wx = ginfo(6) : wy = ginfo(5) 
	screen 1,200,480,,wx,wy
	title ""
	gosub *put_bt
	gsel 0,1

	file_name = ""
	repeat 7
		file_name += strf("%02d",gettime(cnt))
	loop

	stage_name = "名無しのステージ"
	stage_maker = "名無しさん"

	maxcoin = "1"
	maxblast = "1"
	shape = "3"
	sizex = "10.0"
	sizey = "10.0"
	nowobj = 0

	dim obj,100
	dim objtype,1000
	ddim objposx,1000
	ddim objposy,1000
	ddim objsizex,1000
	ddim objsizey,1000
	dim objshape,1000

	qreset : qgravity 0,0 : qmat 0,3,0xffffff

*main
	title "Blast Maker v1.2     配置オブジェクト : "+nowobj+"/100"

	if (form == 0){ shape = 4 }
	if (form == 1){ shape = 36 }

	qcnvaxis mx,my,mousex,mousey,1
	stick key
	if (ginfo_act == 0){
		if (key == 256){
			if (mode == 1){
				if (type == 0){
					if (myobj != 0){ qdel myobj }
					startposx = mx : startposy = my
					qaddpoly myobj,36,startposx,startposy,0,5,5,0,1,2,6
					qmat myobj,3,0x5599ff : qtype myobj,0x100
					qdamper myobj,0,0 : qinertia myobj,0.997,1
				}
				if ((type == 1) & (int(shape) >= 3) & (double(sizex) > 0) & (double(sizey) > 0)){
					objshape(nowobj) = int(shape)
					objposx(nowobj) = mx : objposy(nowobj) = my
					objsizex(nowobj) = double(sizex) : objsizey(nowobj) = double(sizey)
					objtype(nowobj) = 1
					qaddpoly obj(nowobj),objshape(nowobj),objposx(nowobj),objposy(nowobj),0,objsizex(nowobj),objsizey(nowobj),0,objtype(nowobj)
					qmat obj(nowobj),3,0xffffff : qtype obj(nowobj),0x1c0
					nowobj++
				}
				if (type == 2){
					objshape(nowobj) = 36
					objposx(nowobj) = mx : objposy(nowobj) = my
					objsizex(nowobj) = 5.0 : objsizey(nowobj) = 5.0
					objtype(nowobj) = 2
					qaddpoly obj(nowobj),objshape(nowobj),objposx(nowobj),objposy(nowobj),0,objsizex(nowobj),objsizey(nowobj),0,objtype(nowobj)
					qmat obj(nowobj),3,0xffff00 : qtype obj(nowobj),0x1c0
					nowobj++
				}
				if ((type == 3) & (int(shape) >= 3) & (double(sizex) > 0) & (double(sizey) > 0)){
					objshape(nowobj) = int(shape)
					objposx(nowobj) = mx : objposy(nowobj) = my
					objsizex(nowobj) = double(sizex) : objsizey(nowobj) = double(sizey)
					objtype(nowobj) = 4
					qaddpoly obj(nowobj),objshape(nowobj),objposx(nowobj),objposy(nowobj),0,objsizex(nowobj),objsizey(nowobj),0,objtype(nowobj)
					qmat obj(nowobj),3,0xff0000 : qtype obj(nowobj),0x1c0
					nowobj++
				}
			}
			if (mode == 2){
				gosub *delobj
			}
		}
	}
	if key = 128 : assert : end



	if type != mtype : goto *bt1
	mtype = type

	redraw 0
		color 0,0,0 : boxf
		if (mode = 1){
			if ((type == 1) | (type == 3)){
				color 100,100,100
				boxf mousex-int(sizex)*3,mousey-int(sizey)*3,mousex+int(sizex)*3,mousey+int(sizey)*3
			} else {
				color 100,100,100
				boxf mousex-20,mousey-20,mousex+20,mousey+20
			}
		}
		qexec
		qdraw 1
	redraw 1


	await 1
	goto *main


*bt1
	gsel 1 : cls
	title "配置"
	gosub *put_bt
	mode = 1

	objsize 200,20
	mes "\nタイプ"
	combox type,,"プレイヤー\n障害物\nコイン\n敵"
	if ((type == 1) | (type == 3)){
		mes "形"
		combox form,,"四角形\n円"
		mes "Xサイズ（実数可）"
		input sizex
		mes "Yサイズ（実数可）"
		input sizey
	}

	mtype = type

	gsel 0,1
	goto *main


*bt2
	gsel 1 : cls
	title "消去"
	gosub *put_bt
	mode = 2

	gsel 0,1
	goto *main

*bt3
	gsel 1 : cls
	title "設定"
	gosub *put_bt
	mode = 3

	objsize 200,20
	mes "\nステージ名"
	input stage_name,,,12
	mes "ステージ製作者"
	input stage_maker,,,12
	mes "集めるコインの数（１以上）"
	input maxcoin
	mes "ブラスト可能数（１以上）"
	input maxblast

	gsel 0,1
	goto *main

*bt4
	gsel 1 : cls
	title "保存"
	gosub *put_bt
	mode = 4

	dialog "ステージデータを作成します。よろしいですか？",2
	if (stat == 6){
		gosub *make_stage
	}

	gsel 0,1
	goto *main

*bt5
	gsel 1 : cls
	title "終了"
	gosub *put_bt
	mode = 5

	dialog "ステージメーカーを終了しますか？",2
	if (stat == 6){
		exec "blast.exe"
		end
	}

	gsel 0,1
	goto *main


*bt6
	gsel 1 : cls
	title ""
	gosub *put_bt
	mode = 5

	gsel 0,1
	goto *main


*delobj
	qinner inobj,mx,my,-1
	if ((inobj != -1) & (inobj != myobj)){
		qdel inobj
		cou = 0
		repeat 100
			if (obj(cou) == inobj){ break }
			cou++
		loop
		repeat nowobj-cou,cou
			obj(cnt) = obj(cnt+1)
			objshape(cnt) = objshape(cnt+1)
			objposx(cnt) = objposx(cnt+1)
			objposy(cnt) = objposy(cnt+1)
			objsizex(cnt) = objsizex(cnt+1)
			objsizey(cnt) = objsizey(cnt+1)
			objtype(cnt) = objtype(cnt+1)
		loop
		nowobj--
	}

	return



*make_stage
	if (myobj == 0){ dialog "プレイヤーを置いてください。" : goto *main }
	notesel stage_note
	noteadd "StageName="+stage_name
	noteadd "StageMaker="+stage_maker
	noteadd "StartPositionX="+startposx
	noteadd "StartPositionY="+startposy
	noteadd "MaxCoin="+maxcoin
	noteadd "MaxBlast="+maxblast
	noteadd "TotalObj="+nowobj

	repeat nowobj
		noteadd "ObjType("+cnt+")="+objtype(cnt)
		noteadd "ObjPosX("+cnt+")="+objposx(cnt)
		noteadd "ObjPosY("+cnt+")="+objposy(cnt)
		noteadd "ObjSizeX("+cnt+")="+objsizex(cnt)
		noteadd "ObjSizeY("+cnt+")="+objsizey(cnt)
		noteadd "ObjShape("+cnt+")="+objshape(cnt)
	loop

	noteadd "End"

	notesave "./stage/"+file_name+".bsd"

	dialog "書き出しが完了しました。"

	qterm
	goto *reset




*put_bt
	button goto "配置",*bt1
	button goto "消去",*bt2
	button goto "設定",*bt3
	button goto "保存",*bt4
	button goto "終了",*bt5
	return
